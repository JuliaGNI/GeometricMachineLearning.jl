geodesic(A::AbstractVecOrMat) = A
cayley(A::AbstractVecOrMat) = A

geodesic(B::NamedTuple) = apply_toNT(geodesic, B)

@doc raw"""
    geodesic(Y::Manifold, Î”)

Take as input an element of a manifold `Y` and a tangent vector in `Î”` in the corresponding tangent space and compute the geodesic (exponential map).

In different notation: take as input an element ``x`` of ``\mathcal{M}`` and an element of ``T_x\mathcal{M}`` and return ``\mathtt{geodesic}(x, v_x) = \exp(v_x).``


# Examples

```jldoctest
using GeometricMachineLearning

Y = StiefelManifold([1. 0. 0.;]' |> Matrix)
Î” = [0. .5 0.;]' |> Matrix
Yâ‚‚ = geodesic(Y, Î”)

Yâ‚‚' * Yâ‚‚ â‰ˆ [1.;]

# output

true
```

# Implementation

Internally this `geodesic` method calls [`geodesic(::StiefelLieAlgHorMatrix)`](@ref).
"""
function geodesic(Y::Manifold{T}, Î”::AbstractMatrix{T}) where T
    Î»Y = GlobalSection(Y)
    B = global_rep(Î»Y, Î”)
    E = StiefelProjection(B)
    expB = geodesic(B)
    Î»Y * typeof(Y)(expB * E)
end

@doc raw"""
    geodesic(BÌ„::StiefelLieAlgHorMatrix)

Compute the geodesic of an element in [`StiefelLieAlgHorMatrix`](@ref).

# Implementation

Internally this is using:

```math
\mathbb{I} + B'\mathfrak{A}(B', B'')B'',
```

with 

```math
\bar{B} = \begin{bmatrix}
    A & -B^T \\ 
    B & \mathbb{O}
\end{bmatrix} = \begin{bmatrix}  \frac{1}{2}A & \mathbb{I} \\ B & \mathbb{O} \end{bmatrix} \begin{bmatrix}  \mathbb{I} & \mathbb{O} \\ \frac{1}{2}A & -B^T  \end{bmatrix} =: B'(B'')^T.
```

This is using a computationally efficient version of the matrix exponential ``\mathfrak{A}``. 

See [`GeometricMachineLearning.ğ”„`](@ref).
"""
function geodesic(B::StiefelLieAlgHorMatrix)
    T = eltype(B)
    E = StiefelProjection(B)
    unit = one(B.A)
    A_mat = B.A * unit
    BÌ‚ = hcat(vcat(T(.5) * A_mat, B.B), E)
    BÌ„ = hcat(vcat(unit, T(.5) * A_mat), vcat(zero(B.B'), -B.B'))'
    StiefelManifold(one(B) + BÌ‚ * ğ”„(BÌ‚, BÌ„) * BÌ„')
end

@doc raw"""
    geodesic(BÌ„::GrassmannLieAlgHorMatrix)

Compute the geodesic of an element in [`GrassmannLieAlgHorMatrix`](@ref).

This is equivalent to the method of [`geodesic`](@ref) for [StiefelLieAlgHorMatrix](@ref).

See [`geodesic(::StiefelLieAlgHorMatrix)`](@ref).
"""
function geodesic(B::GrassmannLieAlgHorMatrix)
    T = eltype(B)
    E = StiefelProjection(B)
    backend = KernelAbstractions.get_backend(B)
    zero_mat = KernelAbstractions.zeros(backend, T, B.n, B.n)
    BÌ‚ = hcat(vcat(zero_mat, B.B), E)
    BÌ„ = hcat(vcat(one(zero_mat), zero_mat), vcat(zero(B.B'), -B.B'))'
    GrassmannManifold(one(B) + BÌ‚ * ğ”„(BÌ‚, BÌ„) * BÌ„')
end

cayley(B::NamedTuple) = apply_toNT(cayley, B)

@doc raw"""
    cayley(Y::Manifold, Î”)

Take as input an element of a manifold `Y` and a tangent vector in `Î”` in the corresponding tangent space and compute the Cayley retraction.

In different notation: take as input an element ``x`` of ``\mathcal{M}`` and an element of ``T_x\mathcal{M}`` and return ``\mathrm{Cayley}(v_x).`` 

# Examples

```jldoctest
using GeometricMachineLearning

Y = StiefelManifold([1. 0. 0.;]' |> Matrix)
Î” = [0. .5 0.;]' |> Matrix
Yâ‚‚ = cayley(Y, Î”)

Yâ‚‚' * Yâ‚‚ â‰ˆ [1.;]

# output

true
```

See the example in [`geodesic(::Manifold{T}, ::AbstractMatrix{T}) where T`].
"""
function cayley(Y::Manifold{T}, Î”::AbstractMatrix{T}) where T
    Î»Y = GlobalSection(Y)
    B = global_rep(Î»Y, Î”)
    E = StiefelProjection(B)
    cayleyB = cayley(B)
    Î»Y * typeof(Y)(cayleyB * E)
end

@doc raw"""
    cayley(BÌ„::StiefelLieAlgHorMatrix)

Compute the Cayley retraction of `B`.

# Implementation

Internally this is using 

```math
\mathrm{Cayley}(\bar{B}) = \mathbb{I} + \frac{1}{2} B' (\mathbb{I}_{2n} - \frac{1}{2} (B'')^T B')^{-1} (B'')^T (\mathbb{I} + \frac{1}{2} B),
```
with
```math
\bar{B} = \begin{bmatrix}
    A & -B^T \\ 
    B & \mathbb{O}
\end{bmatrix} = \begin{bmatrix}  \frac{1}{2}A & \mathbb{I} \\ B & \mathbb{O} \end{bmatrix} \begin{bmatrix}  \mathbb{I} & \mathbb{O} \\ \frac{1}{2}A & -B^T  \end{bmatrix} =: B'(B'')^T,
```
i.e. ``\bar{B}`` is expressed as a product of two ``N\times{}2n`` matrices.
"""
function cayley(B::StiefelLieAlgHorMatrix)
    T = eltype(B)
    E = StiefelProjection(B)
    ğ•€_small = one(B.A)
    ğ•† = zero(ğ•€_small)
    ğ•€_small2 = hcat(vcat(ğ•€_small, ğ•†), vcat(ğ•†, ğ•€_small))
    ğ•€_big = one(B)
    A_mat = B.A * ğ•€_small
    BÌ‚ = hcat(vcat(T(.5) * A_mat, B.B), E)
    BÌ„ = hcat(vcat(ğ•€_small, T(.5) * A_mat), vcat(zero(B.B'), -B.B'))'

    StiefelManifold((ğ•€_big + T(.5) * BÌ‚ * inv(ğ•€_small2 - T(.5) * BÌ„' * BÌ‚) * BÌ„') * (ğ•€_big + T(.5) * B))
end

@doc raw"""
    cayley(BÌ„::GrassmannLieAlgHorMatrix)

Compute the Cayley retraction of `B`.

This is equivalent to the method of [`cayley`](@ref) for [StiefelLieAlgHorMatrix](@ref).

See [`cayley(::StiefelLieAlgHorMatrix)`](@ref).
"""
function cayley(B::GrassmannLieAlgHorMatrix)
    T = eltype(B)
    E = StiefelProjection(B)
    backend = KernelAbstractions.get_backend(B)
    ğ•† = KernelAbstractions.zeros(backend, T, B.n, B.n)
    ğ•€_small = one(ğ•†)
    ğ•€_small2 = hcat(vcat(ğ•€_small, ğ•†), vcat(ğ•†, ğ•€_small))
    ğ•€_big = one(B)
    BÌ‚ = hcat(vcat(ğ•†, B.B), E)
    BÌ„ = hcat(vcat(ğ•€_small, ğ•†), vcat(zero(B.B'), -B.B'))'

    GrassmannManifold((ğ•€_big + T(.5) * BÌ‚ * inv(ğ•€_small2 - T(.5) * BÌ„' * BÌ‚) * BÌ„') * (ğ•€_big + T(.5) * B))
end